apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: "me.tatarka.retrolambda"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "pondthaitay.googlemapapi.exercises"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warning 'InvalidPackage'
        return void
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "passed", "skipped", "failed", "standardOut"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    return void
}

ext {
    supportVersion = "25.3.1"
    butterknifeVersion = "8.5.1"
    dagger2Version = "2.10"
    rxAndroidVersion = "2.0.1"
    rxJavaVersion = "1.1.6"
    retrofitVersion = "2.2.0"
    okhttp3Version = "3.6.0"
    parcelerVersion = "1.1.6"
    powermockVersion = "1.6.6"
    googlePlayVersion = "10.2.6"
    permisstionDispatcherVersion = "2.4.0"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile "org.powermock:powermock-module-junit4:$powermockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testCompile "org.powermock:powermock-api-mockito:$powermockVersion"
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"

    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    compile 'com.jakewharton.timber:timber:4.5.1'

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile("com.squareup.retrofit2:converter-gson:$retrofitVersion") {
        exclude module: 'com.google.code.gson'
    }
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp3Version"

    compile 'com.google.code.gson:gson:2.8.0'

    compile('com.hwangjr.rxbus:rxbus:2.0.0-beta') {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }

    compile "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    compile "com.google.android.gms:play-services-maps:$googlePlayVersion"

    compile("com.github.hotchemi:permissionsdispatcher:$permisstionDispatcherVersion") {
        exclude module: "support-v13"
    }
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$permisstionDispatcherVersion"
}
